/*
Contains tasks and setup to use for docs generation

Requires in the buildscript

- classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8'
- project `docs-sources` that contains under `src/main/asciidoc` the asciidoc
*/
buildscript {
	repositories { jcenter() }

	dependencies {
		classpath "org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorGradleVersion}"
		classpath "com.github.jruby-gradle:jruby-gradle-plugin:${jrubyVersion}"
		classpath 'org.ajoberstar:grgit:2.2.1'
	}
}

import org.ajoberstar.grgit.Grgit

apply plugin: 'java'
apply plugin: "com.github.jruby-gradle.base"
apply plugin: 'org.asciidoctor.convert'

ext {
	git = Grgit.open()
	ghOrg = "CloudPipelines"
	repo = project.name
	branch = git.branch.current().getName()
	docsBaseUrl = [
		'https://raw.githubusercontent.com',
		ghOrg,
		repo,
		branch
	].join('/')
	scriptsBranch = "master"
	scriptsBaseUrl = [
		'https://raw.githubusercontent.com',
		ghOrg,
		"scripts",
		scriptsBranch
	].join('/')
}

task cleanDocs(type: Delete) {
	delete project(":docs").projectDir.path
}

task createFolder() {
	dependsOn("cleanDocs")
	doLast {
		new File(project.rootDir, "docs").mkdirs()
	}
}

dependencies {
	gems 'rubygems:asciidoctor-diagram:1.5.9'
}

asciidoctor {
	dependsOn = ["createFolder", "jrubyPrepare"]
	sourceDir = file('docs-sources/src/main/asciidoc')
	gemPath = jrubyPrepare.outputDir
	outputDir = file('docs')
	separateOutputDirs = false
	requires = ['asciidoctor-diagram']
	attributes 'source-highlighter': 'prettify',
		toc: 'left',
		toclevels: '4',
		'hide-uri-scheme': '',
		'numbered': '',
		'icons': 'font',
		idprefix: '',
		idseparator: '-',
		safe: 'unsafe',
		parse: 'false',
		nofooter: '',
		sectlinks: 'true',
		'allow-uri-read': '',
		'scripts-base-url': scriptsBaseUrl,
		'intro-root-docs': "${docsBaseUrl}/docs/images/intro",
		'docs-base-url': docsBaseUrl,
		'demo-root-docs': "${docsBaseUrl}/docs/images/demo",
		'jenkins-root-docs' : "${docsBaseUrl}/docs/images/jenkins"
}

// takes the EnvironmentVariables class and parses it
task generateEnvVars() {
	doLast {
		String envVarsClass = new File(project(":job-dsl").projectDir, "src/main/groovy/io/cloudpipelines/common/EnvironmentVariables.groovy").text
		String[] splitClazz = envVarsClass.split("\n")
		List<Number> start = splitClazz.findIndexValues { it.contains("/**") }
		List<Number> end = splitClazz.findIndexValues { it.contains("*/") }
		List<Number> all = (start + end).sort()
		List<List<Number>> startWithEnd = all.collate(2)
		List<String> javaDocs = startWithEnd.collect { splitClazz.getAt(it[0] + 1..it[1] - 1).join("").replaceAll("\\*", "") }.findAll { it.contains("@code") }
		List<List<String>> table = [["Environment variable name", "Description"]]
		String asciiDocTable = javaDocs.inject(table) { List<List<String>> acc, String value ->
			String replaced = value.replace("\n", " ").replace("      ", " ").replaceAll(/\{\@code[ ]?([\w_$.\/-~]+)[ ]?\}/) { allValues, code -> return "`${code}`" }
			int index = replaced.indexOf("-")
			String envName = replaced.substring(0, index).trim()
			String description = replaced.substring(index + 1).trim()
			acc.add([envName, description])
			return acc
		}.collect { "|" + it[0] + " | " + it[1] }.join("\n")
		String header = '''\
// Do not edit this file - it's automatically taken from EnvironmentVariables.groovy file
.Environment variables
[%header,cols=2*]
|===

'''
		String footer = '''
|==='''
		String fullTable = header + asciiDocTable + footer
		new File(project(":docs-sources").projectDir, "src/main/asciidoc/ENVIRONMENT_VARIABLES.adoc").text = fullTable
	}
}

asciidoctor.dependsOn("generateEnvVars")

task generateDocs() {
	description = "Creates documentation for the project"
	dependsOn = ["asciidoctor"]
}
